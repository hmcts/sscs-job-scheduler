plugins {
  id 'java'
  id 'java-library'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.10.RELEASE'
  id 'org.owasp.dependencycheck' version '6.0.3'
  id 'com.github.ben-manes.versions' version '0.36.0'
  id 'org.sonarqube' version '3.0'
  id 'maven-publish'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

def springBootVersion = '2.3.5.RELEASE'


sourceSets {

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

tasks.register('sourcesJar', Jar) {
  dependsOn classes
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

compileJava {
  options.compilerArgs << "-Xlint:unchecked"
}

compileTestJava {
  options.compilerArgs << "-Xlint:unchecked"
}

compileIntegrationTestJava {
  options.compilerArgs << "-Xlint:unchecked"
}

tasks.register('integration', Test) {
  description = "Runs Integration Tests"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

checkstyle {
  toolVersion = '8.4'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDirectory.set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.19.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest]
  reportsDir = layout.buildDirectory.dir("reports/pmd").get().asFile
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

project.tasks.named('sonarqube') {
  dependsOn test, integration
}

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: SSCS job scheduler"
    property "sonar.jacoco.reportPath", layout.buildDirectory.file("jacoco/test.exec")
    property "sonar.jacoco.itReportPath", layout.buildDirectory.file("jacoco/integration.exec")
    property 'sonar.coverage.exclusions', "**/src/main/java/uk/gov/hmcts/reform/sscs/jobscheduler/config/**"
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    retirejs {
      enabled = false
    }
    ossIndex {
      enabled = false
    }
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  mavenCentral()
  mavenLocal()
  mavenCentral()

  maven {
    url 'https://repo.spring.io/libs-milestone'
  }

  // jitpack was last resort but replaced bintray
  // see: https://github.com/jitpack/jitpack.io/issues/1939
  maven { url 'https://jitpack.io' }

}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'sscs-job-scheduler'
      version project.version
    }
  }
}


def versions = [
  reformLogging   : '5.1.7',
  springBoot   : springBootVersion,
]

configurations.configureEach {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
        details.useVersion '2.11.0'
      }
    }
  }
}

dependencyManagement {
  dependencies {

    // CVE-2019-0232, CVE-2019-0199 - command line injections on windows
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.40') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-el'
      entry 'tomcat-embed-websocket'
    }
  }
}

dependencies {
  implementation group: 'org.quartz-scheduler', name: 'quartz', version: '2.3.2'

  implementation group: 'com.github.hmcts', name: 'java-logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts', name: 'reform-api-standards', version: '0.4.0'

  implementation group: 'javax.inject', name: 'javax.inject', version: '1'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot
  implementation group: 'org.springframework', name: 'spring-context-support', version: '5.2.12.RELEASE'

  implementation group: 'org.springframework', name: 'spring-tx', version: '5.2.12.RELEASE'
  implementation group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.1.0'

  testImplementation group: 'junit', name: 'junit', version: 4.12
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.18.1'
  testImplementation group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '2.27.2'
  testImplementation group: 'org.json', name: 'json', version: '20201115'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.6.28'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.6.28'
  testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:5.6.2"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"

  integrationTestImplementation sourceSets.main.runtimeClasspath
}

